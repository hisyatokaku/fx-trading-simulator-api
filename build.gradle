plugins {
	id 'java'
	id 'org.springframework.boot' version '3.1.0'
	id 'io.spring.dependency-management' version '1.1.0'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
	mavenCentral()
}

configurations {
	mithraGenConfig
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation group: 'com.goldmansachs.reladomo', name: 'reladomo', version: '18.1.0'
	testImplementation 'junit:junit:4.13.1'
	testImplementation 'org.testng:testng:7.1.0'
	mithraGenConfig group: 'com.goldmansachs.reladomo', name: 'reladomo', version: '18.1.0'
	mithraGenConfig group: 'com.goldmansachs.reladomo', name: 'reladomogen', version: '18.1.0'
	mithraGenConfig group: 'com.goldmansachs.reladomo', name: 'reladomo-gen-util', version: '18.1.0'
	implementation group: 'org.eclipse.collections', name: 'eclipse-collections', version: '11.1.0'
	implementation group: 'org.springdoc', name: 'springdoc-openapi-starter-webmvc-ui', version: '2.1.0'
	implementation 'com.h2database:h2'
	implementation 'org.postgresql:postgresql'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

sourceSets {
	main {
		java.srcDirs += "src/main/generated-java"
	}
}

task mithraGenerateSources  {
	doLast {
		// Create directories if they don't exist
		file('src/main/generated-java').mkdirs()
		file('src/main/resources/generated-db/sql').mkdirs()
		
		// Set system properties to handle threading issues in containers
		System.setProperty('java.awt.headless', 'true')
		System.setProperty('mithra.generator.singleThread', 'true')
		
		try {
			ant.taskdef(name: 'mithraGen',
					classname:'com.gs.fw.common.mithra.generator.MithraGenerator',
					loaderRef: 'mithraGenerator',
					classpath: configurations.mithraGenConfig.asPath,
			)

			ant.mithraGen(
					xml:'src/main/resources/reladomo/ReladomoClassList.xml',
					generatedDir:'src/main/generated-java',
					nonGeneratedDir:'src/main/java',
					generateConcreteClasses:'true'
			)

			ant.taskdef(name: 'genDdl',
					classname:'com.gs.fw.common.mithra.generator.dbgenerator.MithraDbDefinitionGenerator',  
					loaderRef: 'mithraGenerator',
					classpath: configurations.mithraGenConfig.asPath,
			)

			ant.genDdl(
					xml:'src/main/resources/reladomo/ReladomoClassList.xml',
					generatedDir:'src/main/resources/generated-db/sql',
					databaseType: 'postgres'
			)
		} catch (Exception e) {
			logger.warn("Mithra generation failed: ${e.message}")
			// Create empty marker files so compilation doesn't fail
			new File('src/main/generated-java/.gitkeep').createNewFile()
			throw e
		}
	}
}

task mithraClean {
	delete 'generated-java'
}

clean.dependsOn(mithraClean)

build.dependsOn(mithraGenerateSources)

tasks.named('test') {
	useJUnitPlatform()
}

jar {
	manifest {
		attributes "Main-Class": "com.example.fxtrade.FxTradeApplication"
	}
}